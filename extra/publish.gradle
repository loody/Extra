apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

Properties config = new Properties()
config.load(project.file('../VERSION.properties').newDataInputStream())
def VERSION = config.getProperty("EXTRA_VERSION")
group = rootProject.ext.group
version = VERSION

//项目主页
def siteUrl = 'https://github.com/loody/Extra'    // project homepage
//项目的版本控制地址
def gitUrl = 'https://github.com/loody/Extra.git' // project git
//项目issue
def issuesUrl = 'https://github.com/loody/Extra/issues' // project issue

install {
    repositories.mavenInstaller.pom.project {
        packaging 'aar'
        name 'Extra'
        artifactId "extra"
        description 'A intent data injector for Android .'
        url siteUrl

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id 'loody'
                name 'loody'
                email 'loody128@gmail.com'
            }
        }
        scm {
            connection gitUrl
            developerConnection gitUrl
            url siteUrl
        }
    }
}

task jar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.getDestinationDir()
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    Properties properties = new Properties()
    boolean hasLocalFile = false
    if (project.rootProject.file('local.properties').exists()) {
        hasLocalFile = true
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
    }
    user = hasLocalFile ? properties.getProperty("bintray.user") : ""
    key = hasLocalFile ? properties.getProperty("bintray.apikey") : ""

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "extra"
        websiteUrl = siteUrl
        issueTrackerUrl = issuesUrl
        vcsUrl = gitUrl
        labels = ["Android", "Extra"]
        licenses = ["Apache-2.0"]
        publicDownloadNumbers = true
        publish = true
    }
}